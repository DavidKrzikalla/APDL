! THIS CODE CREATES A MESHED MODEL OF A LONG FIBRE REINFORCED COMPONENT 
! THE BULK OF MATERIAL IS OF A POLYMER REINFORCED BY TRUSS LINKS OF A FIBRE MATERIAL
! THIS MACRO IS SUITABLE TO MODEL BASICALLY ANY ROTATIONAL COMPONENT REINFORCED BY LONG FIBRES FOLLOWING THE ROTATION
! CURRENTLY THE MACRO IS PREPARED TO MODEL A TESTING SPECIMEN, SEE ATTACHED FIGURES IN THE GITHUB REPOSITORY
! AUTHOR: DAVID KRZIKALLA
! MACRO OUTLINE:
! 0) Material models, geometrical and mesh parameters, element settings  
! 1) Cross-sectional (CS) definition in XY plane
! 2) Hard points (HPTs) creation to define possition of fibres*: reading fibre locations**, creating the HPTs, gather the HPTs in the component  
! * The hard points are set within the CS to be respected by the meshing. In these points, later, the fibres are created using LINK180. 
! ** HPTs locations is read from external 'fibre_location_rot.txt' file. The fibre locations data have folowing format: X-coordinate (space, tab or comma) Y-coordinate (Thus the HPTs should be located in XY plane and must lie within the CS defined above). Then, each row corresponds to one HPT
! 3) Rotate HPTs around the axis to generate lines for fibre elements
! 4) Set mesh density for fibres
! 5) Mesh the fibres by LINK180 elements
! 6) Set mesh density for CS and mesh the CS by PLANE182 (QUAD4) elements
! 7) Rotate the CS elements around the axis to create solid part of the component (SOLID185)
! 8) Couple coincident nodes (connection of fibres to bulk material)
! 9) The model is ready for further adjustments or one can proceed to apply boundary conditions etc.

! For further information about model orientation etc., see figures attached in the repository
!----------------------------------------------------------------------------------------------------------------------


/PREP7
!-INITIATION---------------------------------------------------------------------------------------------------------
MAT_NO=1									! MATERIAL MODELS
POLYMER=MAT_NO									

MPTEMP,,,,,,,,  
MPTEMP,1,0  
MPDATA,EX,POLYMER,,1400 					! YOUNG'S MODULUS [MPa]
MPDATA,PRXY,POLYMER,,0.45					! POISSON'S RATIO [-]

ONYX
MAT_NO=2
FIBRE=MAT_NO								! MATERIAL MODEL NUMBER

MPTEMP,,,,,,,,  
MPTEMP,1,0  
MPDATA,EX,FIBRE,,60000 						! YOUNG'S MODULUS [MPa]
MPDATA,PRXY,FIBRE,,0.3						! POISSON'S RATIO [-]

!----------------------------------------------------------------------------------------------------------------------
B=14										! SPECIMEN WIDTH, [mm]
D=4											! SPECIMEN THICKNESS, [mm]

FIBRE_AREA=0.05								! FIBRE CROSS-SECTIONAL AREA, [mm^2] 
CS_EL_SIZE=0.25								! CROSS-SECTION ELEMENT SIZE, [mm](DETERMINE BASED ON DISTANCES AMONG FIBRES WITHIN THE CROSS-SECTION, MESHING ISSUES ARE POSSIBLY SOLVABLE BY SMALLER ELEMENT SIZING)
ROT_DIV=10									! CIRCUMFERENTIAL DIVISIONS, IF THE ANGLE IS MORE THAN 90, THEN THIS VALUE APPLY FOR EACH INDIVIDUAL LINE NOT FOR WHOLE ARC (FOR MORE THAT 90 DEGREES THE ARC IS COMPOSED OF MORE THAN ONE LINE)
ANGLE=120									! ROTATIONAL ANGLE [DEGREES]

!----------------------------------------------------------------------------------------------------------------------
ET,1,LINK180	   							! LINK (TRUSS) ELEMENT SELECTION
ET,2,PLANE182   							! PLANAR ELEMENT SELECTION
ET,3,SOLID185   							! SOLID ELEMENT SELECTION

! 1----------------------------------------------------------------------------------------------------------------------	
K,,B/4,,
K,,B,,
K,,B,D,
K,,B/2,D,
K,,B/2,D/2,
K,,B/4,D/2,

*DO,I,1,5
LSTR,I,I+1
*ENDDO
LSTR,6,1

AL,1,2,3,4,5,6

! 2----------------------------------------------------------------------------------------------------------------------
! CREATE HARD POINTS FOR FIBRES ON C-S AREA BASED ON IMPORTED COORDINATES

/INQUIRE,NO_FIBRES,LINES,'FIBRE_LOCATION_ROT.TXT' 									! READ NO. OF LINES = NO. OF FIBRES
*DIM,FIBRE_LOCATION_TABLE,TABLE,NO_FIBRES,1,,,, 									! PREDEFINE A TABLE
*TREAD,FIBRE_LOCATION_TABLE,'FIBRE_LOCATION_ROT.TXT'								! READ DATA INTO THE PREDEF. TABLE

*DIM,FIBRE_LOCATION,ARRAY,NO_FIBRES,2												! CREATE A BLANK ARRAY
*DO,I,1,2 																			! COPY COLUMN BY COLUMN INTO THE ARRAY
*VFUN,FIBRE_LOCATION(1,I),COPY,FIBRE_LOCATION_TABLE(1,I-1)
*ENDDO

FIBRE_LOCATION_TABLE= 																! DELETE THE INITIAL TABLE

*DO,J,1,NO_FIBRES
HPTCREATE,AREA,1,,COORD,FIBRE_LOCATION(J,1),FIBRE_LOCATION(J,2),
*ENDDO


KSEL,ALL																			! COMPONENT FOR FIBRE HPTs
KSEL,U,LOC,X,B/4
KSEL,U,LOC,X,B/2
KSEL,U,LOC,X,B
CM,FIBRE_KP,KP
ALLSEL,ALL

! ! 3----------------------------------------------------------------------------------------------------------------------	
K,,,,										! ROTATION AXIS KEYPOINTS (BASICALLY GLOBAL Y-AXIS)
K,,,D+5,

*GET,KPMAX,KP,0,NUM,MAXD,,
CMSEL,S,FIBRE_KP							
KSEL,A,,,KPMAX-1
KSEL,A,,,KPMAX

LROTAT,ALL,,,,,,KPMAX-1,KPMAX,ANGLE,

LSEL,ALL									! CREATE A COMPONENT OF FIBRE LINES
LSEL,U,LOC,Z,0
CM,FIBRE_LINE,LINE		
ALLSEL,ALL

! 4----------------------------------------------------------------------------------------------------------------------
CMSEL,S,FIBRE_LINE							
LESIZE,ALL,,,ROT_DIV,,,,,0						

! 5----------------------------------------------------------------------------------------------------------------------
SECTYPE,1,LINK,,FIBRE  						! SET A SECTION FOR FIBRES
SECDATA,FIBRE_AREA,	
SECCONTROL,0,0 

CMSEL,S,FIBRE_LINE							! SELECT FIBRE LINES

TYPE,1   									! SELECT ELEMENT TYPE FOR FIBRE
MAT,FIBRE    								! SELECT MATERIAL FOR FIBRE     
SECNUM,1    								! SELECT SECTION NUMBER FOR FIBRE

LMESH,ALL									! MESH FIBRES LINES BY TRUSS ELEMS
ALLSEL,ALL
/ESHAPE,1

! 6----------------------------------------------------------------------------------------------------------------------
AESIZE,1,CS_EL_SIZE, 

TYPE,2   									! SELECT ELEMENT TYPE 
MAT,POLYMER 								! SELECT MATERIAL    
SECNUM,,    								
MSHAPE,0,2D 								! QUADS, 2D
MSHKEY,										! SET FREE MESHING (BECAUSE OF HPTs CANNOT USE MAPPED MESH)
AMESH,ALL									! MESH THE AREA
ALLSEL,ALL

! 7----------------------------------------------------------------------------------------------------------------------
TYPE,   3   						
EXTOPT,ESIZE,ROT_DIV  								! NO. OF ELEMENTS ALONG CIRUMFERENCE  
EXTOPT,ACLEAR,1 									! CLEAR 2D MESH AFTER ROTATION SWEEP
VROTAT,1, , , , , ,KPMAX-1,KPMAX ,ANGLE,			! ROTATION SWEEP 
ALLSEL,ALL

! 8----------------------------------------------------------------------------------------------------------------------
CPINTF,ALL,0.0001, 							! COUPLE COINCIDENT NODES

ALLSEL,ALL
EPLOT
FINISH

